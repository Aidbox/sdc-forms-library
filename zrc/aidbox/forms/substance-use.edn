{ns aidbox.forms.substance-use
 import #{zenbox aidbox.sdc}

 SubstanceUseDocument
 {:zen/tags #{zen/schema aidbox.sdc/doc aidbox.sdc/rules}
  :zen/desc "Substance Use"
  :type zen/map
  :confirms #{aidbox.sdc/Document}
  :sdc/rules {:dsm-alcohol-score
              (lisp/or
               (lisp/when (lisp/>= (lisp/count (lisp/matcho-filter (lisp/vals (lisp/get-in [:alcohol :dsm]))
                                                                   true))
                                   2)
                 (lisp/count (lisp/matcho-filter (lisp/vals (lisp/get-in [:alcohol :dsm]))
                                                 true)))
               (lisp/+
                (lisp/get-in [:alcohol :dsm :dsm-alcohol-1])
                (lisp/get-in [:alcohol :dsm :dsm-alcohol-2])
                (lisp/get-in [:alcohol :dsm :dsm-alcohol-3])
                (lisp/get-in [:alcohol :dsm :dsm-alcohol-4])
                (lisp/get-in [:alcohol :dsm :dsm-alcohol-5])
                (lisp/get-in [:alcohol :dsm :dsm-alcohol-6])
                (lisp/get-in [:alcohol :dsm :dsm-alcohol-7])
                (lisp/get-in [:alcohol :dsm :dsm-alcohol-8])
                (lisp/get-in [:alcohol :dsm :dsm-alcohol-9])
                (lisp/get-in [:alcohol :dsm :dsm-alcohol-10])
                (lisp/get-in [:alcohol :dsm :dsm-alcohol-11])))
              :dsm-drug-score
              (lisp/or
               (lisp/when (lisp/>= (lisp/count (lisp/matcho-filter (lisp/vals (lisp/get-in [:drug :dsm]))
                                                                   true))
                                   2)
                 (lisp/count (lisp/matcho-filter (lisp/vals (lisp/get-in [:drug :dsm]))
                                                 true)))
               (lisp/+
                (lisp/get-in [:drug :dsm :dsm-drug-1])
                (lisp/get-in [:drug :dsm :dsm-drug-2])
                (lisp/get-in [:drug :dsm :dsm-drug-3])
                (lisp/get-in [:drug :dsm :dsm-drug-4])
                (lisp/get-in [:drug :dsm :dsm-drug-5])
                (lisp/get-in [:drug :dsm :dsm-drug-6])
                (lisp/get-in [:drug :dsm :dsm-drug-7])
                (lisp/get-in [:drug :dsm :dsm-drug-8])
                (lisp/get-in [:drug :dsm :dsm-drug-9])
                (lisp/get-in [:drug :dsm :dsm-drug-10])
                (lisp/get-in [:drug :dsm :dsm-drug-11])))}
  :keys {:nicotine {:type zen/map
                    :keys
                    {:substance-use-nicotine {:type zen/boolean
                                              :zen/desc "**Nicotine:** Any former or current nicotine use?"}
                     :substance-use-nicotine-types {:type zen/vector
                                                    :text "Type of nicotine use"
                                                    :every
                                                    {:text "Type of nicotine use"
                                                     :enum [{:value {:display "Cigarette" :code "nicotine-1"}}
                                                            {:value {:display "Cigar" :code "nicotine-2"}}
                                                            {:value {:display "Chewing Tobacco" :code "nicotine-3"}}
                                                            {:value {:display "Vape" :code "nicotine"}}
                                                            {:value {:display "Other" :code "nicotine-other"}}]
                                                     :zen/desc "Type of nicotine use"}}
                     :substance-use-nicotine-other {:text "Specify Other"
                                                    :type zen/string
                                                    :zen/desc "Specify Other"}
                     :substance-use-nicotine-currently {:zen/desc "Does patient currently smoke?"
                                                        :type zen/boolean}
                     :substance-use-nicotine-frequency {:text "Tobacco use"
                                                        :enum
                                                        [{:value {:display "Every day smoker" :code "LA18976-3"
                                                                  :system  "http://loinc.org"}}
                                                         {:value {:display "Some day smoker" :code "LA18977-1"
                                                                  :system  "http://loinc.org"}}
                                                         {:value {:display "Heavy smoker" :code "LA18981-3"
                                                                  :system  "http://loinc.org"}}
                                                         {:value {:display "Light smoker" :code "LA18982-1"
                                                                  :system  "http://loinc.org"}}
                                                         {:value {:display "Unknown" :code "LA18979-7"
                                                                  :system "http://loinc.org"}}]}
                     :substance-use-nicotine-document-use {:text "Document nicotine use"
                                                           :type zen/string
                                                           :zen/desc "Document nicotine use"}
                     :substance-use-nicotine-20-pack {:text "Does patient have 20 pack-year history or greater?"
                                                      :type zen/boolean
                                                      :zen/desc "Does patient have 20 pack-year history or greater?"}
                     :substance-use-nicotine-cessation-interest {:text "Is the patient interested in smoking/nicotine cessation programs?"
                                                                 :type zen/boolean
                                                                 :zen/desc "Is the patient interested in smoking/nicotine cessation programs?"}}}
         :alcohol {:type zen/map
                   :keys {:substance-use-alcohol {:type zen/boolean
                                                  :zen/desc "**Alcohol:** Any former or current alcohol use?"}
                          :substance-use-alcohol-types {:type zen/vector
                                                        :text "Over the past 12 months, has the patient had any of below:"
                                                        :help "**Standard Drink Volume**

Beer: 12 oz.
Wine: 5 oz.
Liquor: 1.5 oz."
                                                        :every
                                                        {:text "Over the past 12 months"
                                                         :enum [{:value {:display "Beer"
                                                                         :code "53410008"
                                                                         :system "http://snomed.info/sct"}}
                                                                {:value {:display "Wine"
                                                                         :code "35748005"
                                                                         :system "http://snomed.info/sct"}}
                                                                {:value {:display "Liquor"
                                                                         :code "6524003"
                                                                         :system "http://snomed.info/sct"}}]}}
                          :substance-use-alcohol-frequency {:text "Frequency (per week)"
                                                            :confirms #{aidbox.sdc.fhir/quantity}
                                                            :type zen/map
                                                            :keys {:value {:type zen/number
                                                                           :min 0
                                                                           :max 20}}}
                          :substance-use-alcohol-frequency-qualifier {:text "Past or current"
                                                                      :enum [{:value {:display "In the past"
                                                                                      :code "alcohol-frequency-qualifier-1"}}
                                                                             {:value {:display "Current"
                                                                                      :code "alcohol-frequency-qualifier-2"}}]}
                          :substance-use-alcohol-comments {:text "Comments"
                                                           :type zen/string
                                                           :zen/desc "Comments"}
                          ;; DSM-5 Alcohol
                          :dsm-enabled-alcohol {:type zen/boolean
                                                :zen/desc "**Substance Use Disorder Questionnaire**"}
                          :dsm-enabled-alcohol-yes-default {:type zen/boolean
                                                            :sdc/default-value true
                                                            :zen/desc "**Substance Use Disorder Questionnaire**"}
                          :dsm {:type zen/map
                                :keys {:dsm-alcohol-1 {:type zen/boolean
                                                       :text "Drinking in greater amounts than was intended"}
                                       :dsm-alcohol-2 {:type zen/boolean
                                                       :text "A persistent desire or unsuccessful efforts to cut down or control drinking"}
                                       :dsm-alcohol-3 {:type zen/boolean
                                                       :text "A great deal of time is spent to obtain, use, or recover from drinking"}
                                       :dsm-alcohol-4 {:type zen/boolean
                                                       :text "Cravings or a strong desire or urge to drink"}
                                       :dsm-alcohol-5 {:type zen/boolean
                                                       :text "Repeated drinking leads to inability to perform role at work, school, or home"}
                                       :dsm-alcohol-6 {:type zen/boolean
                                                       :text "Drinking continues despite negative consequences in social and interpersonal situations"}
                                       :dsm-alcohol-7 {:type zen/boolean
                                                       :text "Valued social or work-related roles are stopped because of drinking"}
                                       :dsm-alcohol-8 {:type zen/boolean
                                                       :text "Recurrent drinking in situations in which it is physically hazardous."}
                                       :dsm-alcohol-9 {:type zen/boolean
                                                       :text "Continued use despite medical or psychiatric complications likely caused or exacerbated by drinking"}
                                       :dsm-alcohol-10 {:type zen/boolean
                                                        :text "Tolerance: increasing amount is needed to obtain effects"}
                                       :dsm-alcohol-11 {:type zen/boolean
                                                        :text "Withdrawal symptoms occur or patient drinks to prevent withdrawal"}}}
                          :substance-use-alcohol-help-before {:type zen/boolean
                                                              :text "Have you sought help before?"}

                          :substance-use-alcohol-help-now {:type zen/boolean
                                                           :text "Would you like help now?"}}}
         :dsm-alcohol-score {:type zen/number
                             :text "Patient has Alcohol Use Disorder based on DSM-5 Criteria"}

         :drug {:type zen/map
                :keys {:substance-use-drug {:type zen/boolean
                                            :zen/desc "**Marijuana or Illicit Drug:** Any former or current drug use?"}
                       :substance-use-drug-types {:type zen/vector
                                                  :text "Type of drug"
                                                  :every
                                                  {:text "Type of drug"
                                                   :enum [{:value {:display "Marijuana"
                                                                   :code "398705004"
                                                                   :system "http://snomed.info/sct"}}
                                                          {:value {:display "Cocaine"
                                                                   :code "387085005"
                                                                   :system "http://snomed.info/sct"}}
                                                          {:value {:display "Methamphetamine"
                                                                   :code "387499002"
                                                                   :system "http://snomed.info/sct"}}
                                                          {:value {:display "Heroin"
                                                                   :code "387341002"
                                                                   :system "http://snomed.info/sct"}}
                                                          {:value {:display "Sedatives"
                                                                   :code "372614000"
                                                                   :system "http://snomed.info/sct"}}
                                                          {:value {:display "Benzodiazepines"
                                                                   :code "45604009"
                                                                   :system "http://snomed.info/sct"}}
                                                          {:value {:display "Opiates"
                                                                   :code "395888007"
                                                                   :system "http://snomed.info/sct"}}
                                                          {:value {:display "Other"
                                                                   :code "drug-other"}}]
                                                   :zen/desc "Type of drug"}}
                       :substance-use-drug-other {:text "Specify Other"
                                                  :type zen/string
                                                  :zen/desc "Specify Other"}
                       ;; DSM-5 Drug
                       :dsm-enabled-drug {:type zen/boolean
                                          :zen/desc "**Substance Use Disorder Questionnaire**"}
                       :dsm-enabled-drug-yes-default {:type zen/boolean
                                                      :sdc/default-value true
                                                      :zen/desc "**Substance Use Disorder Questionnaire**"}
                       :dsm {:type zen/map
                             :keys {:dsm-drug-1 {:type zen/boolean
                                                 :text "Using the substance in greater amounts than was intended."
                                                 :zen/desc "Using the substance in greater amounts than was intended."}
                                    :dsm-drug-2 {:type zen/boolean
                                                 :text "A persistent desire or unsuccessful efforts to cut down or control use."
                                                 :zen/desc "A persistent desire or unsuccessful efforts to cut down or control use."}
                                    :dsm-drug-3 {:type zen/boolean
                                                 :text "A great deal of time is spent to obtain, use, or recover from the substance."
                                                 :zen/desc "A great deal of time is spent to obtain, use, or recover from the substance."}
                                    :dsm-drug-4 {:type zen/boolean
                                                 :text "Cravings or a strong desire or urge to use the substance."
                                                 :zen/desc "Cravings or a strong desire or urge to use the substance."}
                                    :dsm-drug-5 {:type zen/boolean
                                                 :text "Repeated use leads to inability to perform role at work, school, or home."
                                                 :zen/desc "Repeated use leads to inability to perform role at work, school, or home."}
                                    :dsm-drug-6 {:type zen/boolean
                                                 :text "Use continues despite negative consequences in social and interpersonal situations."
                                                 :zen/desc "Use continues despite negative consequences in social and interpersonal situations."}
                                    :dsm-drug-7 {:type zen/boolean
                                                 :text "Valued social or work-related roles are stopped because of use."
                                                 :zen/desc "Valued social or work-related roles are stopped because of use."}
                                    :dsm-drug-8 {:type zen/boolean
                                                 :text "Recurrent use in situations in which it is physically hazardous."
                                                 :zen/desc "Recurrent use in situations in which it is physically hazardous."}
                                    :dsm-drug-9 {:type zen/boolean
                                                 :text "Continued use despite medical or psychiatric complications likely caused or exacerbated by the substance."
                                                 :zen/desc "Continued use despite medical or psychiatric complications likely caused or exacerbated by the substance."}
                                    :dsm-drug-10 {:type zen/boolean
                                                  :text "Tolerance: increasing amount is needed to obtain effects."
                                                  :zen/desc "Tolerance: increasing amount is needed to obtain effects."}
                                    :dsm-drug-11 {:type zen/boolean
                                                  :text "Withdrawal symptoms occur or patient takes substance to prevent withdrawal."
                                                  :zen/desc "Withdrawal symptoms occur or patient takes substance to prevent withdrawal."}}}
                       :substance-use-drug-help-before {:type zen/boolean
                                                        :text "Have you sought help before?"
                                                        :zen/desc "Have you sought help before?"}

                       :substance-use-drug-help-now {:type zen/boolean
                                                     :text "Would you like help now?"
                                                     :zen/desc "Would you like help now?"}
                       ;; DSM-5 Drug End
                       }}
         :dsm-drug-score {:type zen/number
                          :sdc-type aidbox.sdc/calculated
                          :zen/desc "Patient has Substance Use Disorder based on DSM-5 Criteria"}}}

 SubstanceUseLayout
 {:zen/tags #{aidbox.sdc/Layout aidbox.sdc/rules}
  :document SubstanceUseDocument
  :engine aidbox.sdc/Hiccup
  :sdc/rules {:enable-nicotine (lisp/get-in [:nicotine :substance-use-nicotine])
              :enable-alcohol (lisp/get-in [:alcohol :substance-use-alcohol])
              :enable-other-nicotine (lisp/> (lisp/count (lisp/matcho-filter (lisp/get-in [:nicotine :substance-use-nicotine-types])
                                                                             {:code "nicotine-other"})) 0)

              :enabled-dsm-alcohol (lisp/get-in [:alcohol :dsm-enabled-alcohol])
              :enable-dsm-alcohol-help-questions (lisp/>= (lisp/get :dsm-alcohol-score) 2)

              :enable-other-drug (lisp/> (lisp/count (lisp/matcho-filter (lisp/get-in [:drug :substance-use-drug-types])
                                                                         {:code "drug-other"})) 0)
              :possible-drug-dsm (lisp/> (lisp/-
                                          (lisp/count (lisp/get-in [:drug :substance-use-drug-types]))
                                          (lisp/count (lisp/matcho-filter (lisp/get-in [:drug :substance-use-drug-types])
                                                                          {:code "398705004"})))
                                         0)
              :enabled-dsm-drug (lisp/if (lisp/get :possible-drug-dsm)
                                  (lisp/get-in [:drug :dsm-enabled-drug-yes-default])
                                  (lisp/get-in [:drug :dsm-enabled-drug]))
              :enable-dsm-drug-help-questions (lisp/>= (lisp/get :dsm-drug-score) 2)}
  :layout
  {:type aidbox.sdc/col
   :children
   [;; Nicotine Section
    {:bind [:nicotine :substance-use-nicotine]}
    {:type aidbox.sdc/fields
     :children
     [{:type aidbox.sdc/fields
       :sdc/display-when (lisp/get :enable-nicotine)
       :children
       [{:bind [:nicotine :substance-use-nicotine-types]}
        {:bind [:nicotine :substance-use-nicotine-other]
         :sdc/display-when (lisp/get :enable-other-nicotine)}
        {:bind [:nicotine :substance-use-nicotine-currently]}
        {:bind [:nicotine :substance-use-nicotine-frequency]
         :sdc/display-when (lisp/get-in [:nicotine :substance-use-nicotine-currently])}
        {:bind [:nicotine :substance-use-nicotine-document-use]}
        {:bind [:nicotine :substance-use-nicotine-20-pack]}
        {:bind [:nicotine :substance-use-nicotine-cessation-interest]}]}]}

    ;; Alcohol Section
    {:bind [:alcohol :substance-use-alcohol]}
    {:type aidbox.sdc/fields
     :children
     [{:type aidbox.sdc/fields
       :sdc/display-when (lisp/get :enable-alcohol)
       :children
       [{:bind [:alcohol :substance-use-alcohol-types]}
        {:bind [:alcohol :substance-use-alcohol-frequency]}
        {:bind [:alcohol :substance-use-alcohol-frequency-qualifier]}
        {:bind [:alcohol :substance-use-alcohol-comments]}
        {:bind [:alcohol :dsm-enabled-alcohol]
         :sdc/display-when (lisp/or
                            (lisp/not (lisp/get-in [:alcohol :substance-use-alcohol-frequency :value]))
                            (lisp/< (lisp/get-in [:alcohol :substance-use-alcohol-frequency :value]) 7))}
        {:bind [:alcohol :dsm-enabled-alcohol-yes-default]
         :sdc/display-when (lisp/>= (lisp/get-in [:alcohol :substance-use-alcohol-frequency :value]) 7)}
        {:type aidbox.sdc/fields
         :children
         [{:type aidbox.sdc/fields
           :sdc/display-when (lisp/if
                              (lisp/or
                               (lisp/not (lisp/get-in [:alcohol :substance-use-alcohol-frequency :value]))
                               (lisp/< (lisp/get-in [:alcohol :substance-use-alcohol-frequency :value]) 7))
                               (lisp/get-in [:alcohol :dsm-enabled-alcohol])
                               (lisp/get-in [:alcohol :dsm-enabled-alcohol-yes-default]))
           :children [{:type aidbox.sdc/fields
                       :children
                       [{:type aidbox.sdc/label,
                         :label "**Over the past 12 months, has the patient had any of below:**"}
                        {:type aidbox.sdc/row,
                         :children [{:bind [:alcohol :dsm :dsm-alcohol-1]}]}
                        {:type aidbox.sdc/row,
                         :children [{:bind [:alcohol :dsm :dsm-alcohol-2]}]}
                        {:type aidbox.sdc/row,
                         :children [{:bind [:alcohol :dsm :dsm-alcohol-3]}]}
                        {:type aidbox.sdc/row,
                         :children [{:bind [:alcohol :dsm :dsm-alcohol-4]}]}
                        {:type aidbox.sdc/row,
                         :children [{:bind [:alcohol :dsm :dsm-alcohol-5]}]}
                        {:type aidbox.sdc/row,
                         :children [{:bind [:alcohol :dsm :dsm-alcohol-6]}]}
                        {:type aidbox.sdc/row,
                         :children [{:bind [:alcohol :dsm :dsm-alcohol-7]}]}
                        {:type aidbox.sdc/row,
                         :children [{:bind [:alcohol :dsm :dsm-alcohol-8]}]}
                        {:type aidbox.sdc/row,
                         :children [{:bind [:alcohol :dsm :dsm-alcohol-9]}]}
                        {:type aidbox.sdc/row,
                         :children [{:bind [:alcohol :dsm :dsm-alcohol-10]}]}
                        {:type aidbox.sdc/row,
                         :children [{:bind [:alcohol :dsm :dsm-alcohol-11]}]}]}
                      {:bind [:dsm-alcohol-score]
                       :label "Patient has Alcohol Use Disorder based on DSM-5 Criteria"}]}]}

        {:type aidbox.sdc/row
         :sdc/display-when (lisp/get :enable-dsm-alcohol-help-questions)
         :children
         [{:bind [:alcohol :substance-use-alcohol-help-before]}
          {:bind [:alcohol :substance-use-alcohol-help-now]}]}]}]}

    ;; Drug Section
    {:bind [:drug :substance-use-drug]}
    {:type aidbox.sdc/fields
     :children
     [{:type aidbox.sdc/col
       :sdc/display-when (lisp/get-in [:drug :substance-use-drug])
       :children
       [{:bind [:drug :substance-use-drug-types]}
        {:sdc/display-when (lisp/get :enable-other-drug)
         :bind [:drug :substance-use-drug-other]}
        {:sdc/display-when (lisp/not (lisp/= (lisp/get :possible-drug-dsm) true))
         :bind [:drug :dsm-enabled-drug]}
        {:sdc/display-when (lisp/= (lisp/get :possible-drug-dsm) true)
         :bind [:drug :dsm-enabled-drug-yes-default]}
        {:type aidbox.sdc/fields
         :children
         [{:type aidbox.sdc/fields
           :sdc/display-when (lisp/get-in [:enabled-dsm-drug])
           :children
           [{:type aidbox.sdc/label
             :label "**Over the past 12 months, has the patient had any of below:**"}
            {:type aidbox.sdc/row
             :children [{:bind [:drug :dsm :dsm-drug-1]}]}
            {:type aidbox.sdc/row
             :children [{:bind [:drug :dsm :dsm-drug-2]}]}
            {:type aidbox.sdc/row
             :children [{:bind [:drug :dsm :dsm-drug-3]}]}
            {:type aidbox.sdc/row
             :children [{:bind [:drug :dsm :dsm-drug-4]}]}
            {:type aidbox.sdc/row
             :children [{:bind [:drug :dsm :dsm-drug-5]}]}
            {:type aidbox.sdc/row
             :children [{:bind [:drug :dsm :dsm-drug-6]}]}
            {:type aidbox.sdc/row
             :children [{:bind [:drug :dsm :dsm-drug-7]}]}
            {:type aidbox.sdc/row
             :children [{:bind [:drug :dsm :dsm-drug-8]}]}
            {:type aidbox.sdc/row
             :children [{:bind [:drug :dsm :dsm-drug-9]}]}
            {:type aidbox.sdc/row
             :children [{:bind [:drug :dsm :dsm-drug-10]}]}
            {:type aidbox.sdc/row
             :children [{:bind [:drug :dsm :dsm-drug-11]}]}
            {:bind [:dsm-drug-score]
             :label "Patient has Substance Use Disorder based on DSM-5 Criteria"}]}]}
        {:type aidbox.sdc/row
         :sdc/display-when (lisp/get :enable-dsm-drug-help-questions)
         :children
         [{:bind [:drug :substance-use-drug-help-before]}
          {:bind [:drug :substance-use-drug-help-now]}]}]}]}]}}

 SubstanceUseLaunch
 {:zen/tags #{aidbox.sdc/Launch}
  :document SubstanceUseDocument
  :params {:encounter-id {:type zen/string}}
  :populate-engine aidbox.sdc/LispPopulate
  :populate {:author    (lisp/get-in [:ctx :user])
             :encounter {:id (lisp/get-in [:params :encounter-id]) :resourceType "Encounter"}
             :patient   (lisp/sql {:select [:#> :resource [:subject]]
                                   :from :Encounter
                                   :where [:= :id (lisp/get-in [:params :encounter-id])]})
             :nicotine (lisp/sql {:select [:#> :resource [:nicotine]]
                                  :from :SDCDocument
                                  :where [:and
                                          [:= [:#>> :resource [:status]] "completed"]
                                          [:= [:#>> :resource [:form :form]] "aidbox.forms.substance-use/SubstanceUseForm"]]
                                  :order-by [:pg/desc :ts]
                                  :limit 1})
             :alcohol (lisp/sql {:select [:#> :resource [:alcohol]]
                                 :from :SDCDocument
                                 :where [:and
                                         [:= [:#>> :resource [:status]] "completed"]
                                         [:= [:#>> :resource [:form :form]] "aidbox.forms.substance-use/SubstanceUseForm"]]
                                 :order-by [:pg/desc :ts]
                                 :limit 1})}}

 SubstanceUseFinalize
 {:zen/tags #{aidbox.sdc/Finalize zen/schema}
  :document SubstanceUseDocument
  :export-engine aidbox.sdc/LispExport
  :create [;; Extract tobacco smoking status
           (lisp/if
            (lisp/not (lisp/nil? (lisp/get-in [:nicotine :substance-use-nicotine])))
             {:resourceType "Observation"
              :status "final"
              :subject (lisp/get :patient)
              :encounter (lisp/get :encounter)
              :category [{:coding [{:system "http://terminology.hl7.org/CodeSystem/observation-category"
                                    :code "social-history"}]}]
              :effective {:dateTime (lisp/date)}
              :issued (lisp/date)
              :code {:text "Tobacco smoking status"
                     :coding [{:system "http://loinc.org" :display "Tobacco smoking status NHIS" :code "72166-2"}]}
              :value
              {:CodeableConcept
               {:coding
                [(lisp/cond
                   (lisp/not (lisp/get-in [:nicotine :substance-use-nicotine]))
                   {:system "http://loinc.org" :code "LA18978-9" :display "Never smoker"}

                   (lisp/not (lisp/get-in [:nicotine :substance-use-nicotine-currently]))
                   {:system "http://loinc.org" :code "LA15920-4" :display "Former smoker"}

                   :else (lisp/get-in [:nicotine :substance-use-nicotine-frequency]))]}}})

           ;; Extract Details of alcohol drinking behavior
           (lisp/for [lisp/i (lisp/get-in [:alcohol :substance-use-alcohol-types])]
             {:resourceType "Observation"
              :status "final"
              :encounter (lisp/get :encounter)
              :subject (lisp/get :patient)
              :category [{:coding [{:system "http://terminology.hl7.org/CodeSystem/observation-category"
                                    :code "social-history"}]}]
              :effective {:dateTime (lisp/date)}
              :issued (lisp/date)
              :code {:text "SHX Alcohol type"
                     :coding [{:system "http://snomed.info/sct"
                               :code "363905002"
                               :display "Details of alcohol drinking behavior (observable entity)"}]}

              :value
              {:CodeableConcept
               {:coding
                [lisp/i]}}})

           ;; Extract Alcohol use disorder
           (lisp/if
            (lisp/not (lisp/nil? (lisp/get-in [:alcohol :substance-use-alcohol])))
             {:resourceType "Observation"
              :status "final"
              :subject (lisp/get :patient)
              :encounter (lisp/get :encounter)
              :category [{:coding [{:system "http://terminology.hl7.org/CodeSystem/observation-category"
                                    :code "social-history"}]}]
              :effective {:dateTime (lisp/date)}
              :issued (lisp/date)
              :code {:coding [{:system "https://loinc.org"
                               :code "74043-1"
                               :display "Alcohol use disorder"}]}

              :value
              {:CodeableConcept
               {:coding [(lisp/if (lisp/and (lisp/get :dsm-alcohol-score)
                                            (lisp/>= (lisp/get :dsm-alcohol-score) 2))
                           {:code "LA14495-8"
                            :display "True"
                            :system "https://loinc.org"}
                           {:code "LA14496-6"
                            :display "False"
                            :system "https://loinc.org"})]}}})

;; Extract drug usage
           (lisp/for [lisp/i (lisp/get-in [:drug :substance-use-drug-types])]
             (lisp/when (lisp/not (lisp/= (lisp/get-in lisp/i [:code]) "drug-other"))
               {:resourceType "Observation"
                :status "final"
                :encounter (lisp/get :encounter)
                :subject (lisp/get :patient)
                :category [{:coding [{:system "http://terminology.hl7.org/CodeSystem/observation-category"
                                      :code "social-history"}]}]
                :effective {:dateTime (lisp/date)}
                :issued (lisp/date)
                :code {:text "Substance Abuse"
                       :coding [{:system "http://snomed.info/sct"
                                 :code "363908000"
                                 :display "Details of drug misuse behavior (observable entity)"}]}

                :value
                {:CodeableConcept
                 {:coding
                  [lisp/i]}}}))]}

 SubstanceUseForm
 {:zen/tags #{aidbox.sdc/Form}
  :title    "Substance Use"
  :document SubstanceUseDocument
  :layout   SubstanceUseLayout
  :launch   SubstanceUseLaunch
  :finalize SubstanceUseFinalize}}
